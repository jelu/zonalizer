#!/usr/bin/env perl

use common::sense;

use EV;
use AnyEvent;
use Log::Log4perl;
use Lim::Plugin::Zonalizer;
use DB_File;
use HTTP::Status;
use AnyEvent::HTTP;
use Lim::RPC::TLS;

use Data::Dumper;

STDOUT->autoflush(1);

Log::Log4perl->init( \q(
log4perl.logger                   = DEBUG, Screen
log4perl.appender.Screen          = Log::Log4perl::Appender::Screen
log4perl.appender.Screen.stderr   = 0
log4perl.appender.Screen.layout   = Log::Log4perl::Layout::PatternLayout
log4perl.appender.Screen.layout.ConversionPattern = %d %F [%L] %p: %m%n
) );
my $logger = Log::Log4perl->get_logger;

Lim::LoadConfig($ARGV[0]);
Lim::UpdateConfig;

Lim::Config->{zonalizer}->{iana_interval} ||= 86400;

my ($state, %state, %tld, $ongoing, $after);
$state = tie %state, 'DB_File', $ARGV[1];

my $cv = AnyEvent->condvar;
my ( $update_w, $read_w );

$update_w = AnyEvent->timer( after => 0, interval => Lim::Config->{zonalizer}->{iana_interval}, cb => sub {
    Lim::DEBUG and $logger->debug('Getting IANA');

    my $req; $req = http_get Lim::Config->{zonalizer}->{iana_url}, sub {
        my ($body, $hdr) = @_;

        unless ($hdr->{Status} == 200) {
            Lim::ERR and $logger->error('Failed to get ', Lim::Config->{zonalizer}->{iana_url}, ' [', $hdr->{Status}, ( defined $hdr->{'content-type'} ? ' / '.$hdr->{'content-type'} : () ), ']');
            undef $req;
            return;
        }

        my ( $tlds, $type, $company, $last ) = ( 0, 0, 0 );

        foreach (split(/[\r\n]+/omg, $body)) {
            if ($type and /td>([^<]+)/o) {
                $tld{$last}->{type} = $1;
                $type = 0;
                $company = 1;
                next;
            }
            if ($company and /td>([^<]+)/o) {
                $tld{$last}->{company} = $1;
                $company = 0;
                $tlds++;
                next;
            }
            if (/href="\/domains\/root\/db\/([^\.]+\.)html">\.([^<]+)/o) {
                $tld{$1} = {
                    idn => $1,
                    tld => $2
                };
                $last = $1;
                $type = 1;
                next;
            }
        }

        Lim::DEBUG and $logger->debug($tlds, ' TLDs found');

        undef $req;
    };
});

$read_w = AnyEvent->timer( after => 5, interval => 1, cb => sub {
    if ( $ongoing ) {
        return;
    }
    unless ( scalar %tld ) {
        return;
    }

    Lim::DEBUG and $logger->debug('Fetch results', ($after ? ' after '.$after : ''));

    $ongoing = 1;
    Lim::Plugin::Zonalizer->Client->ReadAnalysis({
        version => 1,
        sort => 'created',
        direction => 'ascending',
        ( $after ? ( after => $after ) : ())
    },
    sub {
        my ( $c, $r ) = @_;

        unless ( $c->Successful ) {
            Lim::ERR and $logger->error('Zonalizer error while reading: ', $c->Error->toString);
            return;
        }

        unless ( exists $r->{analysis} ) {
            return;
        }

        my @influx;
        foreach my $analyze ( ref($r->{analysis}) eq 'ARRAY' ? @{$r->{analysis}} : $r->{analysis} ) {
            unless ( exists $tld{$analyze->{fqdn}} ) {
                next;
            }

            my $fqdn = 'fqdn='.$analyze->{fqdn};
            my $time = $analyze->{created}.'000000000';
            push(@influx,
                'critical,'.$fqdn.' value='.$analyze->{summary}->{critical}.' '.$time,
                'error,'.$fqdn.' value='.$analyze->{summary}->{error}.' '.$time,
                'warning,'.$fqdn.' value='.$analyze->{summary}->{warning}.' '.$time,
                'notice,'.$fqdn.' value='.$analyze->{summary}->{notice}.' '.$time
            );
        }

        if ( ref($r->{paging}) eq 'HASH' and ref($r->{paging}->{cursors}) ) {
            $after = $r->{paging}->{cursors}->{after};
        }
        else {
            $after = undef;
        }

        if ( @influx ) {
            my $req; $req = http_post Lim::Config->{zonalizer}->{influx_url}, join("\n", @influx), sub {
                my ($body, $hdr) = @_;

                $ongoing = 0;

                unless ($hdr->{Status} >= 200 and $hdr->{Status} < 300) {
                    Lim::ERR and $logger->error('Failed to store metrics [', $hdr->{Status}, ']');
                }

                undef $req;
                return;
            };
            return;
        }

        $ongoing = 0;
    },
    {
        uri => Lim::Config->{zonalizer}->{uri}
    });
});

sub fatal {
    $logger->fatal(@_);
    $update_w = undef;
    $cv->send;
}

$cv->recv;
